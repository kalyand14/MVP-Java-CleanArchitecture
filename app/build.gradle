apply from: '../jacoco.gradle'

apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.android.basics"
        minSdkVersion 21
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    android {
        sourceSets {
            String sharedTestDir = 'src/sharedTest/java'
            test {
                java.srcDirs += sharedTestDir
            }
            androidTest {
                java.srcDirs += sharedTestDir
            }
        }
    }


    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                testLogging {
                    events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // App dependencies
    implementation deps.app_compat
    implementation deps.cardview
    implementation deps.material
    implementation deps.recyclerview
    implementation deps.constraint_layout
    implementation deps.timber
    implementation deps.annotations
    implementation deps.espresso.idling_resource
    implementation deps.room.runtime
    annotationProcessor deps.room.compiler

    // Architecture Components
    implementation deps.arch_core.runtime
    implementation deps.lifecycle.runtime
    annotationProcessor deps.lifecycle.compiler

    // Dependencies for local unit tests
    testImplementation deps.junit
    testImplementation deps.mockito.core
    testImplementation deps.robolectric
    testImplementation deps.atsl.core
    testImplementation deps.atsl.runner
    testImplementation deps.atsl.ext_junit
    testImplementation deps.espresso.core
    testImplementation deps.espresso.intents
    testImplementation deps.truth
    testImplementation deps.hamcrest

    // Dependencies for Android unit tests
    androidTestImplementation deps.junit
    androidTestImplementation deps.mockito.core

    // AndroidX Test - JVM testing
    testImplementation deps.atsl.ext_junit
    testImplementation deps.atsl.runner
    testImplementation deps.atsl.rules
    // Once https://issuetracker.google.com/127986458 is fixed this can be testImplementation
    implementation deps.atsl.core


    // AndroidX Test - Instrumented testing
    androidTestImplementation deps.atsl.ext_junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.room.testing
    androidTestImplementation deps.espresso.idling_concurrent
}
